import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.androidMultiplatformLibrary)
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.ksp)
    alias(libs.plugins.ktlint)
}

group = "io.github.kingg22"
version = libs.versions.ktorgen.version.get()

kotlin {
    applyDefaultHierarchyTemplate()

    androidLibrary {
        namespace = "${group}.ktorgen.sample"
        compileSdk = Integer.parseInt(libs.versions.android.compileSdk.get())
        minSdk = Integer.parseInt(libs.versions.android.minSdk.get())
        compilerOptions.jvmTarget.set(JvmTarget.JVM_1_8)
    }

    jvm {
        compilerOptions.jvmTarget.set(JvmTarget.JVM_1_8)
    }

    js {
        browser()
        nodejs()
        binaries.library()
    }

    wasmJs {
        browser()
        nodejs()
        binaries.library()
    }

    // Tiers are in accordance with <https://kotlinlang.org/docs/native-target-support.html>
    // Tier 1
    // iOS
    iosArm64()
    iosX64()
    iosSimulatorArm64()
    // macOs
    macosX64()
    macosArm64()
    // Tier 2
    // watchOS
    watchosArm32()
    watchosArm64()
    watchosX64()
    watchosSimulatorArm64()
    // tvOS
    tvosArm64()
    tvosX64()
    tvosSimulatorArm64()
    // linux
    linuxX64()
    linuxArm64()
    // Tier 3
    // android native
    androidNativeArm32()
    androidNativeArm64()
    androidNativeX86()
    androidNativeX64()
    // windows
    mingwX64()
    watchosDeviceArm64()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.ktorgen.annotations)
                implementation(libs.ktor.client.core)
            }
        }
    }
}

dependencies {
    // ksp(projects.compiler) // KMP project don't use this, only jvm or android kotlin projects

    // don't apply on common main because we're going to generate on each platform. STILL FAILING
    kspCommonMainMetadata(libs.ktorgen.compiler)
    add("kspAndroid", libs.ktorgen.compiler)
    add("kspJvm", libs.ktorgen.compiler)
    add("kspJs", libs.ktorgen.compiler)
    add("kspWasmJs", libs.ktorgen.compiler)
    add("kspAndroidNativeX64", libs.ktorgen.compiler)
    add("kspAndroidNativeX86", libs.ktorgen.compiler)
    add("kspAndroidNativeArm32", libs.ktorgen.compiler)
    add("kspAndroidNativeArm64", libs.ktorgen.compiler)
    add("kspLinuxX64", libs.ktorgen.compiler)
    add("kspLinuxArm64", libs.ktorgen.compiler)
    add("kspMingwX64", libs.ktorgen.compiler)
    add("kspIosX64", libs.ktorgen.compiler)
    add("kspIosArm64", libs.ktorgen.compiler)
    add("kspIosSimulatorArm64", libs.ktorgen.compiler)
    add("kspTvosArm64", libs.ktorgen.compiler)
    add("kspMacosArm64", libs.ktorgen.compiler)
    add("kspMacosX64", libs.ktorgen.compiler)
}

ksp {
    // pass argument to compiler
    arg("ktorgen_check_type", "0")
    arg("ktorgen_print_stacktrace_on_exception", "false")
}
