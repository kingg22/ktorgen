import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    alias(libs.plugins.androidMultiplatformLibrary)
    alias(libs.plugins.dokka)
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.mavenPublish)
}

group = "io.github.kingg22"
description = "Kotlin annotations for generating Ktor Client interface implementations using the KtorGen KSP processor."
version = libs.versions.ktorgen.version.get()

kotlin {
    compilerOptions {
        languageVersion.set(KotlinVersion.KOTLIN_2_0)
        apiVersion.set(languageVersion)
        extraWarnings.set(true)
        allWarningsAsErrors.set(true)
    }

    abiValidation {
        enabled.set(true)
        klib {
            enabled.set(true)
            keepUnsupportedTargets.set(true)
        }
    }

    // Maintain the list of targets in sync with ktor client core
    // https://ktor.io/docs/client-supported-platforms.html
    // https://github.com/ktorio/ktor/blob/main/build-logic/src/main/kotlin/ktorbuild/targets/KtorTargets.kt
    applyDefaultHierarchyTemplate()

    androidLibrary {
        namespace = "${group}.ktorgen.annotations"
        compileSdk = Integer.parseInt(libs.versions.android.compileSdk.get())
        minSdk = Integer.parseInt(libs.versions.android.minSdk.get())

        compilerOptions.jvmTarget.set(JvmTarget.JVM_1_8)

        optimization.consumerKeepRules.publish = true
        optimization.consumerKeepRules.files.add(project.file("consumer-rules.pro"))
    }

    jvm {
        compilerOptions.jvmTarget.set(JvmTarget.JVM_1_8)
    }

    js {
        browser()
        nodejs()
        binaries.library()
    }

    wasmJs {
        browser()
        nodejs()
        binaries.library()
    }

    // Tiers are in accordance with <https://kotlinlang.org/docs/native-target-support.html>
    // Tier 1
    // iOS
    iosArm64()
    iosX64()
    iosSimulatorArm64()
    // macOs
    macosX64()
    macosArm64()
    // Tier 2
    // watchOS
    watchosArm32()
    watchosArm64()
    watchosX64()
    watchosSimulatorArm64()
    // tvOS
    tvosArm64()
    tvosX64()
    tvosSimulatorArm64()
    // linux
    linuxX64()
    linuxArm64()
    // Tier 3
    // android native
    androidNativeArm32()
    androidNativeArm64()
    androidNativeX86()
    androidNativeX64()
    // windows
    mingwX64()
    watchosDeviceArm64()

    dependencies {
        implementation(libs.jetbrains.annotations)
    }
}

dokka {
    dokkaPublications.configureEach {
        outputDirectory.set(new File(rootDir, "docs/api"))
    }
    dokkaSourceSets.commonMain {
        skipEmptyPackages.set(true)
        skipDeprecated.set(false)
        reportUndocumented.set(true)
        enableJdkDocumentationLink.set(true)
        enableKotlinStdLibDocumentationLink.set(true)
        suppressGeneratedFiles.set(true)
        includes.from("README.md")

        // adds source links that lead to this repository, allowing readers
        // to easily find source code for inspected declarations
        sourceLink {
            localDirectory.set(file("src/commonMain/kotlin"))
            remoteUrl("https://github.com/kingg22/ktorgen/tree/main/annotations/src/commonMain/kotlin")
            remoteLineSuffix.set("#L")
        }
    }
}

tasks.check {
    dependsOn(tasks.checkLegacyAbi)
}

mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    coordinates(group.toString(), "ktorgen-annotations", version.toString())

    pom {
        name.set("KtorGen - Annotations")
        description.set(project.description)
        inceptionYear.set("2025")
        url.set("https://github.com/kingg22/ktorgen")
        licenses {
            license {
                name.set("The Apache Software License, Version 2.0")
                url.set("https://www.apache.org/licenses/LICENSE-2.0")
                distribution.set("repo")
            }
        }
        developers {
            developer {
                id.set("kingg22")
                name.set("Rey Acosta (Kingg22)")
                url.set("https://github.com/kingg22")
            }
        }
        scm {
            url.set("https://github.com/kingg22/ktorgen")
            connection.set("scm:git:git://github.com/kingg22/ktorgen.git")
            developerConnection.set("scm:git:ssh://git@github.com/kingg22/ktorgen.git")
        }
    }
}
