plugins {
    // this is necessary to avoid the plugins to be loaded multiple times
    // in each subproject's classloader
    alias(libs.plugins.androidLibrary) apply false
    alias(libs.plugins.androidMultiplatformLibrary) apply false
    alias(libs.plugins.dokka) apply false
    alias(libs.plugins.kotlinJvm) apply false
    alias(libs.plugins.kotlinMultiplatform) apply false
    alias(libs.plugins.kotlinxKover) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.ktlint)
    alias(libs.plugins.mavenPublish) apply false
    alias(libs.plugins.sonarqube)
    alias(libs.plugins.changelog)
}

group = "io.github.kingg22"
version = libs.versions.ktorgen.version.get()
def libs = libs

subprojects {
    apply(plugin: libs.plugins.ktlint.get().pluginId)
    apply(plugin: libs.plugins.sonarqube.get().pluginId)

    group = "io.github.kingg22"

    ktlint {
        version.set(libs.versions.ktlint.pinterest)
    }

    sonar {
        // ignora completamente estos módulos
        if (project.name != "annotations" && project.name != "compiler") {
            isSkipProject = true
        }

        properties {
            // evita incluir coverage de módulos que no tienen tests
            if (project.name == "annotations") {
                property("sonar.sources", "src/commonMain")
                property("sonar.coverage.exclusions", "**/*")
            }

            if (project.name == "compiler") {
                property("sonar.sources", "src/main")
                property("sonar.tests", "src/test")
                property(
                    "sonar.coverage.jacoco.xmlReportPaths",
                    project.layout.buildDirectory.file("reports/kover/report.xml").get().asFile.absolutePath,
                )
            }
        }
    }
}

sonar.properties {
    property("sonar.projectKey", "kingg22_ktorgen")
    property("sonar.organization", "kingg22")
    property("sonar.projectName", "ktorgen")
    property("sonar.projectVersion", libs.versions.ktorgen.version.get())
    property("sonar.gradle.scanAll", false) // Conflict with submodules config
    property("sonar.scanner.skipJreProvisioning", true)
    property("sonar.scanner.javaExePath", "${System.getProperty("java.home")}/bin/java")
}

tasks.sonar {
    onlyIf {
        System.getProperty("java.home") != null && System.getenv("SONAR_TOKEN") != null
    }
}

ktlint {
    version.set(libs.versions.ktlint.pinterest)
}

changelog {
    // Default values:
    path.set(file("docs/CHANGELOG.md").canonicalPath)
    // version.set(libs.versions.ktorgen.version)
    // keepUnreleasedSection = true
    // header = provider { "[${version.get()}] - ${date()}" }
    // unreleasedTerm = "[Unreleased]"
    // groups = listOf("Added", "Changed", "Deprecated", "Removed", "Fixed", "Security")
    // combinePreReleases = true
    versionPrefix.set("")
    repositoryUrl.set("https://github.com/kingg22/ktorgen")
}
