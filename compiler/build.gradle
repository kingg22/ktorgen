import kotlinx.kover.gradle.plugin.dsl.AggregationType
import kotlinx.kover.gradle.plugin.dsl.CoverageUnit
import org.jetbrains.kotlin.gradle.dsl.JvmDefaultMode
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.sonarqube.gradle.SonarTask

plugins {
    alias(libs.plugins.kotlinJvm)
    alias(libs.plugins.kotlinxKover)
    alias(libs.plugins.ktlint)
    alias(libs.plugins.mavenPublish)
}

group = "io.github.kingg22"
description = "A Kotlin Symbol Processing (KSP) that generates Ktor Client implementations from annotated interfaces."
version = libs.versions.ktorgen.version.get()

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xcontext-parameters", "-Xcontext-sensitive-resolution", "-Xnested-type-aliases")
        optIn.addAll("kotlin.contracts.ExperimentalContracts", "io.github.kingg22.ktorgen.core.KtorGenExperimental")
        languageVersion.set(KotlinVersion.KOTLIN_2_2)
        apiVersion.set(languageVersion)
        jvmDefault.set(JvmDefaultMode.NO_COMPATIBILITY)
        jvmTarget.set(JvmTarget.JVM_11)
        extraWarnings.set(true)
        allWarningsAsErrors.set(true)
    }
}

dependencies {
    implementation(projects.annotations)
    implementation(libs.kotlin.reflect)
    implementation(libs.ksp.api)
    implementation(libs.kotlin.poet.ksp)
    implementation(libs.ktor.http)

    testImplementation(libs.androidx.room.compiler.testing)
    testImplementation(libs.kotlin.test)
    testImplementation(libs.google.truth)
    testRuntimeClasspath(libs.ktor.client.core)
}

kover {
    reports.total {
        verify {
            rule("Basic Line Coverage") {
                minBound(60, CoverageUnit.LINE, AggregationType.COVERED_PERCENTAGE)
            }

            rule("Basic Branch Coverage") {
                minBound(20, CoverageUnit.BRANCH, AggregationType.COVERED_PERCENTAGE)
            }
        }
        filters.excludes {
            annotatedBy("${group}.ktorgen.KtorGenWithoutCoverage")
        }
        xml {
            xmlFile.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
        }
    }
}

ktlint {
    version.set(libs.versions.ktlint.pinterest)
}

tasks.test {
    useJUnitPlatform()
}

tasks.compileTestKotlin {
    compilerOptions.optIn.add("androidx.room.compiler.processing.ExperimentalProcessingApi")
}

rootProject.tasks.named("sonar", SonarTask.class) {
    dependsOn(tasks.koverXmlReport)
}

mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    coordinates(group.toString(), "ktorgen-compiler", version.toString())

    pom {
        name.set("KtorGen â€“ KSP Annotation Processor")
        description.set(project.description)
        inceptionYear.set("2025")
        url.set("https://github.com/kingg22/ktorgen")
        licenses {
            license {
                name.set("The Apache Software License, Version 2.0")
                url.set("https://www.apache.org/licenses/LICENSE-2.0")
                distribution.set("repo")
            }
        }
        developers {
            developer {
                id.set("kingg22")
                name.set("Rey Acosta (Kingg22)")
                url.set("https://github.com/kingg22")
            }
        }
        scm {
            url.set("https://github.com/kingg22/ktorgen")
            connection.set("scm:git:git://github.com/kingg22/ktorgen.git")
            developerConnection.set("scm:git:ssh://git@github.com/kingg22/ktorgen.git")
        }
    }
}
